import TensorCore.TC.CanonicalFormatDefs
import TensorCore.TC.Canonical



namespace TensorCore

/-- Uncorrected output, error, and machine-width contract for any profile. -/
theorem profile_contract (p : Profile) (F carryBits : ℕ) (hF : p.alignMantissaBits = F)
    (hc : p.products + 1 ≤ 2 ^ carryBits) (x : BlockInput p) (t : BlockTrace)
    (h : evalBlock x = .ok t) :
    exactDot x = some t.block.exactDot ∧
    round32 .truncate t.block.accumulator = some t.output.bits ∧
    absQ (t.block.exactDot - t.output.value) <
      ((p.products + 1 : ℕ) : ℚ) * pow2 t.block.alignGridExponent +
        pow2 (outputUlpExponent t.output.bits) ∧
    t.block.machineAccumulator (F + 3 + carryBits) = t.block.accumulator := by
  have hp := evalBlock_prepared h
  have hlen := (prepare_terms_bounded hp).1
  have hshape : x.products.length = p.products := by
    unfold evalBlock at h
    split at h <;> simp_all
  have herr := evalBlock_error_bound h
  have hw := evalBlock_machineAccumulator h F carryBits hF hc
  refine ⟨by simp [exactDot, hp], evalPrepared_output (evalBlock_evalPrepared h), ?_, ?_⟩
  · simpa [hlen, hshape] using herr
  · have he : F + 2 + carryBits + 1 = F + 3 + carryBits := by omega
    simpa [he] using hw

theorem bf16Fp32_contract (K extra carryBits : ℕ) (floor : Option ℤ)
    (x : BlockInput (bf16Fp32Profile K extra floor)) (t : BlockTrace)
    (h : evalBlock x = .ok t) (hc : K + 1 ≤ 2 ^ carryBits) :
    exactDot x = some t.block.exactDot ∧
    round32 .truncate t.block.accumulator = some t.output.bits ∧
    absQ (t.block.exactDot - t.output.value) <
      ((K + 1 : ℕ) : ℚ) * pow2 t.block.alignGridExponent +
        pow2 (outputUlpExponent t.output.bits) ∧
    t.block.machineAccumulator (26 + extra + carryBits) = t.block.accumulator := by
  have := profile_contract (bf16Fp32Profile K extra floor) (23 + extra) carryBits rfl hc x t h
  have he : 23 + extra + 3 + carryBits = 26 + extra + carryBits := by omega
  simpa [he, bf16Fp32Profile] using this

theorem tf19Fp32_contract (K extra carryBits : ℕ) (floor : Option ℤ)
    (x : BlockInput (tf19Fp32Profile K extra floor)) (t : BlockTrace)
    (h : evalBlock x = .ok t) (hc : K + 1 ≤ 2 ^ carryBits) :
    exactDot x = some t.block.exactDot ∧
    round32 .truncate t.block.accumulator = some t.output.bits ∧
    absQ (t.block.exactDot - t.output.value) <
      ((K + 1 : ℕ) : ℚ) * pow2 t.block.alignGridExponent +
        pow2 (outputUlpExponent t.output.bits) ∧
    t.block.machineAccumulator (26 + extra + carryBits) = t.block.accumulator := by
  have := profile_contract (tf19Fp32Profile K extra floor) (23 + extra) carryBits rfl hc x t h
  have he : 23 + extra + 3 + carryBits = 26 + extra + carryBits := by omega
  simpa [he, tf19Fp32Profile] using this

/-- The BF16 descriptors are the BF16 profiles embedded as invocations. -/
theorem a100BF16_descriptor : a100BF16Invocation = a100BF16F32.toInvocation 24 := rfl
theorem hopperBF16_descriptor : hopperBF16Invocation = hopperBF16F32.toInvocation 25 := rfl

theorem bf16Fp32_invocation_compatible (K extra : ℕ) (floor : Option ℤ)
    (x : BlockInput (bf16Fp32Profile K extra floor)) :
    invocationBits (x.toInvocation (23 + extra)) =
      (evalBlock x).toOption.map (fun t => t.output.bits) :=
  legacy_invocation_bits x (23 + extra) (by change bf16.WellFormed; decide) rfl

/-- A padded TF32 register word decodes as its value word under the `tf19` profile. -/
theorem tf32Register_decode (K extra : ℕ) (floor : Option ℤ) (w : tf32Register.Word)
    (hp : tf32Padded w = true) :
    tf32Register.decode w = (tf19Fp32Profile K extra floor).decode (tf32Unpack w) := by
  have hp' : w.toNat % 2 ^ 13 = 0 := by simpa [tf32Padded] using hp
  have hlt : w.toNat < 2 ^ 32 := w.isLt
  have hq : w.toNat / 2 ^ 13 % 2 ^ 19 = w.toNat / 2 ^ 13 := Nat.mod_eq_of_lt (by omega)
  show (if (w.toNat % 2 ^ 13 != 0) = true then none
    else (classifyNat tf19 (w.toNat / 2 ^ 13)).finite) =
    (classifyNat tf19 ((BitVec.ofNat 19 (w.toNat / 2 ^ 13)).toNat)).finite
  rw [BitVec.toNat_ofNat, hq, hp']
  rfl

theorem tf32Register_decode_unpadded (w : tf32Register.Word) (hp : tf32Padded w = false) :
    tf32Register.decode w = none := by
  have hp' : (w.toNat % 2 ^ 13 != 0) = true := by
    unfold tf32Padded at hp
    simpa using hp
  show (if (w.toNat % 2 ^ 13 != 0) = true then none
    else (classifyNat tf19 (w.toNat / 2 ^ 13)).finite) = none
  rw [if_pos hp']

theorem tf32_mapM (K extra : ℕ) (floor : Option ℤ)
    (ps : List (tf32Register.Word × tf32Register.Word))
    (hp : ∀ pair ∈ ps, tf32Padded pair.1 = true ∧ tf32Padded pair.2 = true) :
    ps.mapM (fun (a, b) => do return (← tf32Register.decode a, ← tf32Register.decode b)) =
      (tf32UnpackPairs ps).mapM (fun (a, b) => do
        return (← (tf19Fp32Profile K extra floor).decode a,
          ← (tf19Fp32Profile K extra floor).decode b)) := by
  induction ps with
  | nil => rfl
  | cons pair rest ih =>
    rcases pair with ⟨a, b⟩
    obtain ⟨ha, hb⟩ := hp (a, b) (by simp)
    simp only [tf32UnpackPairs, List.map_cons, List.mapM_cons]
    rw [tf32Register_decode K extra floor a ha, tf32Register_decode K extra floor b hb]
    have ih' := ih (fun q hq => hp q (by simp [hq]))
    simp only [tf32UnpackPairs] at ih'
    rw [ih']

/-- Descriptor evaluation of a TF32 path on explicit register words. -/
def tf32InvocationBits (K F : ℕ) (floor : Option ℤ)
    (ps : List (tf32Register.Word × tf32Register.Word)) (c : F32) : Option F32 :=
  invocationBits (p := alignedInvocation tf32Register K F floor) ⟨ps, c⟩

theorem tf32_prepare (K F extra : ℕ) (floor : Option ℤ)
    (ps : List (tf32Register.Word × tf32Register.Word)) (c : F32)
    (hp : ∀ pair ∈ ps, tf32Padded pair.1 = true ∧ tf32Padded pair.2 = true) :
    prepareInvocation (p := alignedInvocation tf32Register K F floor) ⟨ps, c⟩ =
      (prepare (⟨tf32UnpackPairs ps, c⟩ : BlockInput (tf19Fp32Profile K extra floor))).map
        (fun b => (⟨b.products, b.c⟩ : PreparedInvocation (alignedInvocation tf32Register K F floor))) := by
  show (do
    let c ← decode32 c
    let ps ← ps.mapM fun (a, b) => do
      return (← tf32Register.decode a, ← tf32Register.decode b)
    return (⟨ps, c⟩ : PreparedInvocation (alignedInvocation tf32Register K F floor))) = _
  rw [tf32_mapM K extra floor ps hp]
  change (do
    let c ← decode32 c
    let ps ← prepareProducts (tf19Fp32Profile K extra floor) (tf32UnpackPairs ps)
    return (⟨ps, c⟩ : PreparedInvocation (alignedInvocation tf32Register K F floor))) = _
  unfold prepare
  cases decode32 c <;>
    cases prepareProducts (tf19Fp32Profile K extra floor) (tf32UnpackPairs ps) <;> rfl

private theorem finite32_none' {bits : F32} (hd : decode32 bits = none) : finite32 bits = none := by
  unfold finite32
  split
  · rfl
  · rename_i d h
    rw [hd] at h
    contradiction

private theorem finite32_some' {bits : F32} {d : Decoded} (hd : decode32 bits = some d) :
    finite32 bits = some ⟨bits, d, hd⟩ := by
  unfold finite32
  split
  · rename_i h
    rw [hd] at h
    contradiction
  · rename_i d' h
    rw [hd] at h
    cases Option.some.inj h
    rfl

set_option maxRecDepth 4096 in
/-- A padded-word descriptor and the profile of its value layout compute the same block. -/
theorem padded_prepared_bits (f : Format) (pad K F : ℕ) (floor : Option ℤ)
    (ps : List (Decoded × Decoded)) (c : Decoded) :
    (evalInvocationPrepared (p := alignedInvocation ⟨⟨f, .ieee⟩, pad⟩ K F floor)
      ⟨ps, c⟩).toOption.map (fun t => t.output.bits) =
      (evalPrepared ⟨⟨f, K, F, floor⟩, ps, c⟩).toOption.map (fun t => t.output.bits) := by
  simp only [evalInvocationPrepared, accumulateInvocation, alignedInvocation,
    PreparedInvocation.alignedBlock, runRoundings]
  simp only [RoundingStage.roundValue, evalPrepared, ite_true]
  rw [roundBinary_fp32 .truncate]
  cases hr : round32 .truncate (PreparedBlock.mk ⟨f, K, F, floor⟩ ps c).accumulator with
  | none => rfl
  | some bits =>
    cases hd : decode32 bits with
    | none => simp [finite32_none' hd, finiteBinary_none hd]; rfl
    | some d => simp [finite32_some' hd, finiteBinary_some hd]; rfl

/-- On padded register words, a TF32 descriptor computes the `tf19` profile's block. -/
theorem tf32_invocation_bits (K extra : ℕ) (floor : Option ℤ)
    (ps : List (tf32Register.Word × tf32Register.Word)) (c : F32)
    (hp : ∀ pair ∈ ps, tf32Padded pair.1 = true ∧ tf32Padded pair.2 = true) :
    tf32InvocationBits K (23 + extra) floor ps c =
      (evalBlock (⟨tf32UnpackPairs ps, c⟩ : BlockInput (tf19Fp32Profile K extra floor))).toOption.map
        (fun t => t.output.bits) := by
  have hv : (alignedInvocation tf32Register K (23 + extra) floor).Valid := by
    refine ⟨?_, ?_, rfl, ?_, trivial⟩
    · change tf19.WellFormed; decide
    · change fp32.WellFormed; decide
    · change fp32.WellFormed; decide
  unfold tf32InvocationBits invocationBits
  rw [evalInvocation, if_neg (by intro hn; exact hn hv)]
  unfold evalBlock
  dsimp only [alignedInvocation]
  have hlen : (tf32UnpackPairs ps).length = ps.length := by simp [tf32UnpackPairs]
  have hK : (tf19Fp32Profile K extra floor).products = K := rfl
  rw [hlen, hK]
  by_cases hs : (ps.length != K) = true
  · rw [if_pos hs, if_pos hs]; rfl
  · rw [if_neg hs, if_neg hs, tf32_prepare K (23 + extra) extra floor ps c hp]
    dsimp only [prepare]
    cases decode32 c with
    | none => rfl
    | some c' =>
      cases prepareProducts (tf19Fp32Profile K extra floor) (tf32UnpackPairs ps) with
      | none => rfl
      | some ps' => exact padded_prepared_bits tf19 13 K (23 + extra) floor ps' c'

/-- The same statement for any descriptor input. -/
theorem tf32_input_bits (K extra : ℕ) (floor : Option ℤ)
    (x : InvocationInput (alignedInvocation tf32Register K (23 + extra) floor))
    (hp : ∀ pair ∈ x.products, tf32Padded pair.1 = true ∧ tf32Padded pair.2 = true) :
    invocationBits x = (evalBlock (tf32Input x extra)).toOption.map (fun t => t.output.bits) :=
  tf32_invocation_bits K extra floor x.products x.c hp

theorem a100TF32_descriptor :
    a100TF32Invocation = alignedInvocation tf32Register 4 (23 + 1) (some (-132)) := rfl
theorem hopperTF32Wmma_descriptor :
    hopperTF32WmmaInvocation = alignedInvocation tf32Register 4 (23 + 2) (some (-133)) := rfl
theorem hopperTF32Mma_descriptor :
    hopperTF32MmaInvocation = alignedInvocation tf32Register 8 (23 + 2) (some (-133)) := rfl

end TensorCore
