import TensorCore.TC.Canonical

namespace TensorCore

/-- Truncation and nearest-even rounding return decodable finite FP32 words for inputs within range. -/
theorem round32_finite_exists (mode : RoundingMode) (x : ℚ) (hr : absQ x ≤ maxFinite32) :
    ∃ bits d, round32 mode x = some bits ∧ decode32 bits = some d := by
  by_cases hz : x = 0
  · subst x
    refine ⟨0, ⟨0, 0, 0⟩, ?_, by decide⟩
    cases mode <;> decide +kernel
  · obtain ⟨bits, hb, hv, _, _⟩ := round32_nonzero_spec mode x hz hr
    cases hd : decode32 bits with
    | none => simp [value32, hd] at hv
    | some d => exact ⟨bits, d, hb, hd⟩

theorem evalPrepared_total (b : PreparedBlock) (hr : absQ b.accumulator ≤ maxFinite32) :
    ∃ t, evalPrepared b = .ok t := by
  obtain ⟨bits, d, hb, hd⟩ := round32_finite_exists .truncate b.accumulator hr
  have hf : finite32 bits = some ⟨bits, d, hd⟩ := by
    unfold finite32
    split
    · rename_i he
      rw [hd] at he
      contradiction
    · rename_i d' he
      rw [hd] at he
      cases Option.some.inj he
      rfl
  exact ⟨⟨b, ⟨bits, d, hd⟩⟩, by simp [evalPrepared, hb, hf]⟩

/-- Exact accepted domain: correct shape, finite decoded operands, and in-range aligned accumulator. -/
theorem evalBlock_success_iff (p : Profile) (x : BlockInput p) :
    (∃ t, evalBlock x = .ok t) ↔
      x.products.length = p.products ∧
        ∃ b, prepare x = some b ∧ absQ b.accumulator ≤ maxFinite32 := by
  constructor
  · rintro ⟨t, ht⟩
    have hp := evalBlock_prepared ht
    have hr := round32_range (evalPrepared_output (evalBlock_evalPrepared ht))
    refine ⟨?_, t.block, hp, hr⟩
    unfold evalBlock at ht
    split at ht <;> simp_all
  · rintro ⟨hshape, b, hp, hr⟩
    obtain ⟨t, ht⟩ := evalPrepared_total b hr
    exact ⟨t, by simpa [evalBlock, hshape, hp] using ht⟩

end TensorCore
